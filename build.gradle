plugins {
    id 'maven-publish'
    id 'java-test-fixtures'
    id 'org.jetbrains.kotlin.jvm' version '1.8.22'
    id 'com.exactpro.th2.gradle.publish' version '0.2.3'
}

group = 'com.exactpro.th2'
version = release_version

ext {
    junitVersion = '5.11.0'
}

repositories {
    mavenCentral()
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }

    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

test {
    useJUnitPlatform()
}

configurations {
    testCompileOnly.extendsFrom compileOnly
    testRuntimeOnly.extendsFrom compileOnly
}

dependencies {
    // TODO: Use this dependency util extract th2 transport into separate library
    compileOnly("com.exactpro.th2:common:5.14.0-dev") {
        because("Transport message classes are used in utilities methods")
    }

    implementation "org.jetbrains:annotations"
    implementation "org.slf4j:slf4j-api"

    implementation 'io.github.oshai:kotlin-logging:5.1.4'

    implementation "io.prometheus:simpleclient"

    testImplementation("com.fasterxml.jackson.core:jackson-databind") {
        because('provide ability to use object mapper in components')
    }
    testImplementation "org.mockito.kotlin:mockito-kotlin:5.4.0"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testFixturesImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}